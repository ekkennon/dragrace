5 OPEN "I",#1,"DRAG-COL.DG1"
6 INPUT #1, COL1,COL2,COL3:CLOSE #1
7 COLOR COL1,COL2,COL3
10 REM
13 DIM SV(2)
14 DIM VL(2)
15 DIM L(2,5)
16 DIM P(2,9)
17 DIM O(2,3)
20 CLS
30 OPEN "I",#1,"TINTVAL.DG1"
40 INPUT #1,TNVL
50 CLOSE #1
51 PRINT:PRINT:PRINT"THIS IS A TUTORIAL PROGRAM.....ADVANCED VERSIONS HAVE OTHER OPTIONS"
52 PRINT:PRINT:PRINT
55 PRINT "BE SURE 'CAPS LOCK' IS ON - THIS GAME ONLY ACCEPTS CAPITAL LETTERS"
60 INPUT "DO YOU WANT THE TUTORIAL TEXT ON (Y/N)";TUT$
80 GOTO 14000
890 CLS
891 T=0:T1=0:T2=0:H(1)=0:H(2)=0:PRINT USING "H is ####";H(2)
892 R=0:Q(1)=0:Q(2)=0:S(1)=0:S(2)=0:B(1)=0:B(2)=0:X(1)=0:X(2)=0
893 NTR(1)=N(1):NTR(2)=N(2):L14=L(1,4):L13=L(1,3):L24=L(2,4):L23=L(2,3):TR1$=TR$(1):TR2$=TR$(2)
894 IF TR$(1)="SLIP" THEN TR$(1)="":IF TR$(2)="SLIP" THEN TR$(2)=""
895 P(1,8)=0:P(2,8)=0:O(1,2)=0:O(2,2)=0
900 FOR Z11 = 1 TO 2
904 IF TR$(Z11)="PG" THEN L(Z11,4)=3.62:L=Z11:GOSUB 10020
910 GOSUB 3170
932 PRINT USING "LANE # IS PROGRAMMED TO SHIFT AT ###0 RPM";Z11;P(Z11,2)/10
933 PRINT "IF YOU WANT TO CHANGE THIS, INPUT YOUR DESIRED RPM"
940 INPUT Z10
950 IF Z10>0 THEN 960 ELSE 1025
960 IF P(Z11,2)>5999 AND P(Z11,1)>P(Z11,3)*.96 THEN XZ1=1.11 ELSE XZ1=1.04
970 IF Z10<P(Z11,2)*1.2 THEN 990
980 IF Z10>XZ1*((P(Z11,1)/P(Z11,3))*6200) THEN 1010 ELSE 990
990 P(Z11,2)=Z10
1000 GOTO 1025
1010 PRINT "RPM TOO HIGH FOR THIS ENGINE - TRY AGAIN"
1020 GOTO 932
1025 REM
1030 NEXT Z11
1040 PRINT"THIS IS A TEST OF MACHINES--BOTH DRIVERS ARE EQUAL"
1050 KEY OFF:PRINT:PRINT:PRINT
1060 LOCATE 25:PRINT USING "### HP  ### CI #### LBS  #.## | LOW TIME  ##.## | ### HP  ### CI #### LBS  #.## ";P(1,1);P(1,3);O(1,1);L(1,5);LOT;P(2,1);P(2,3);O(2,1);L(2,5)
1070 PRINT:PRINT "                                      THREE"
1080 FOR Z9=1 TO 500:NEXT Z9
1090 PRINT:PRINT "                                       TWO"
1100 FOR Z9=1 TO 500:NEXT Z9
1110 PRINT:PRINT "                                       ONE"
1120 PRINT "                              ||  GO  GO  GO   ||
1130 FOR J=1 TO 2
1135 P(J,9)=P(J,1)
1140 IF P(J,4)=0 THEN P(J,4)=1
1150 P(J,1)=P(J,1)*P(J,4)
1160 M(J)=(O(J,1)*1.5+P(J,1)+P(J,3)+(D(J)*W(J)))/33
1170 REM: B IS MAX ACCEL W/O BURNING
1180 B(J)=15+28*W(J)*D(J)/((W(J)+6)*(D(J)+1))
1190 NEXT J
1200 FOR T=0 TO 100
1210 FOR T1 = 1 TO 10
1211 FOR TMR=1 TO TNVL
1212 FOR TM2=1 TO TNVL
1218 NEXT TM2
1219 NEXT TMR
1220 FOR J=1 TO 2
1230 IF J=1 THEN 2060
1240 IF J=2 THEN 2220
1250 REM: R IS FOR RPM
1260 R=60*S(J)*E(J)/(3.14159*D(J))
1270 IF J=2 THEN H(2)=R
1280 IF J=1 THEN H(1)=R
1290 REM: new burnout routine at 1400
1300 GOTO 2390
1310 REM: A=ACCELERATION
1320 IF Q(J)<>0 THEN 2590
1330 IF R=0 THEN 1400
1340 IF T1<(P(J,1)*2+P(J,3))*E(J)*.001*(P(J,9)/2)/(O(J,1)*.8)THEN 1400
1350 IF T1<(P(J,1)*2+P(J,3))*E(J)*.001*(P(J,9)/2)/(O(J,1)*.8)THEN 1400
1360 IF J=1 THEN PRINT USING "STOPS BURNING AT  #### FEET   ||     ##.##     ||            #### FEET";X(1);T+(T1/10);X(2)
1370 IF J=2 THEN PRINT USING "           #### FEET          ||     ##.##     ||STOPS BURNING AT  #### FEET";X(1);T+(T1/10);X(2)
1380 Q(J)=1
1390 GOTO 2590
1400 REM: BURN ACCEL
1410 A=((P(J,1)+P(J,3))*(E(J)*(2.9/D(J))))/((O(J,1)/12)+(S(J)*.7))
1420 REM: S IS FT IN FT/SEC
1440 IF A>W(J)*5 THEN A=W(J)*4.5
1450 XX1=(P(J,1)/P(J,3))*6000
1455 IF T<2 THEN A=A*1.2*(3000/O(J,1))
1460 IF H(J)>XX1*.34 THEN 1490 ELSE 1470
1470 S(J)=S(J)+A*.037
1480 S(J)=S(J)+A*((H(J)/(XX1*(5/6)))*.025):GOTO 1540
1490 IF H(J)>(XX1*(5/6)) THEN 1510 ELSE 1500
1500 S(J)=S(J)+A*((H(J)/(XX1*(5/6)))*.053):GOTO 1540
1510 IF H(J)>XX1 THEN 1520 ELSE 1530
1520 S(J)=S(J)+A*((.049*(XX1/H(J)))):GOTO 1550
1530 S(J)=S(J)+A*((.052*((XX1*(5/6))/H(J))))
1540 S(J)=S(J)+(A*((H(J)/XX1)*.053))
1550 REM: X IS DISTANCE IN FT
1560 IF B(J)=1 THEN 1570 ELSE 1590
1570 X(J)=X(J)+S(J)*.075
1580 GOTO 1600
1590 X(J)=X(J)+S(J)*.098
1600 NEXT J
1610 REM: TEST FOR FINISH
1620 IF X(1)<5280/4 AND X(2)< 5280/4 THEN 1870
1630 IF X(1)>X(2) THEN 1780
1640 T3=(X(2)-5280/4)/S(2)
1650 T=T+(T1/10)-T3
1660 X(2)=X(2)-S(2)*T3
1670 X(1)=X(1)-S(1)*T3
1680 PRINT USING "           #### FEET          ||     ##.## --->||  LANE 2 WINNER";X(1);T
1682 WS=S(2)*3600/5280
1684 WIN2=WIN2+1
1685 LWIN=2:LWINT=T
1690 PRINT USING " ###.## MPH    #### RPM       ||               ||   ###.## MPH  #### RPM";S(1)*3600/5280;H(1);S(2)*3600/5280;H(2)
1700 REM
1705 INPUT "PRESS ENTER TO CONTINUE";I$
1710 ROUND=ROUND+1
1720 N(1)=NTR(1):N(2)=NTR(2):L(1,4)=L14:L(1,3)=L13:L(2,4)=L24:L(2,3)=L23:TR$(1)=TR1$:TR$(2)=TR2$
1730 GOTO 11000
1780 T3=(X(1)-5280/4)/S(1)
1790 T=T+(T1/10)-T3
1800 X(1)=5200/4
1810 X(2)=X(2)-S(2)*T3
1820 PRINT USING "       LANE  1   WINNER       ||<--- ##.##     ||            #### FEET";T;X(2)
1830 PRINT USING "###.## MPH  #### RPM          ||               ||    ###.## MPH   #### RPM";S(1)*3600/5280;H(1);S(2)*3600/5280;H(2)
1832 WS=S(1)*3600/5280
1834 WIN1=WIN1+1
1835 LWIN=1:LWINT=T
1840 REM
1850 PRINT
1860 GOTO 1705
1870 NEXT T1
1880 PRINT USING "###.## MPH #### FEET #### RPM ||     ##.##     ||###.## MPH  #### FEET #### RPM";S(1)*3600/5280;X(1);H(1);T+1;S(2)*3600/5280;X(2);H(2)
1890 NEXT T
1900 END
1910 REM: POWERGLIDE
1920 IF N(J)=1 THEN 2000
1930 IF L(J,2)<1.89 THEN 2040
1940 IF H(J)>((P(J,5)+P(J,7))/3) THEN 1970 ELSE L(J,2)=L(J,2)-((H(J)/200000!)/(88/S(J)))
1960 GOTO 2040
1970 IF L(J,2)>2.5 THEN L(J,2)=L(J,2)-((H(J)/(P(J,5)-500))*(1.9/L(J,2))*.2) ELSE L(J,2)=L(J,2)-.02
1990 GOTO 2040
2000 REM:N(J)=1
2010 IF PGS(J)=1 THEN 2030
2020 PGS(J)=1:L(J,1)=1.18
2030 IF L(J,1)>1.07 THEN L(J,1)=L(J,1)-.001
2040 E(J)=L(J,N(J))*L(J,5)
2050 RETURN
2060 IF TR$(J)="TH" THEN GOSUB 6100
2061 IF TR$(J)="HYD" THEN GOSUB 6300
2064 IF H(1)=0 THEN 2140
2070 IF TR$(1)="PG" THEN GOSUB 6200
2072 IF TR$(J)="TH1" THEN GOSUB 6100
2075 IF TR$(J)="SLIP" THEN GOSUB 6000
2080 IF H(1)<P(1,2)*1.0001 THEN 1250
2090 IF N(1)>1 THEN 2115
2100 IF H(1)>(P(1,2)*1.07)THEN 2190
2110 IF N(1)=1 THEN 1250
2115 IF X(2)>1320 THEN 1780
2120 IF J=1 THEN PRINT USING"SHIFTING - #### RPM, ### MPH  ||     ##.##     ||            #### FEET ";H(J);S(J)*3600/5280;T+(T1/10);X(2)
2130 N(1)=N(1)-1
2140 IF N(1)=4 THEN E(1)=L(1,4)*L(1,5)
2150 IF N(1)=3 THEN E(1)=L(1,3)*L(1,5)
2160 IF N(1)=2 THEN E(1)=L(1,2)*L(1,5)
2170 IF N(1)=1 THEN E(1)=L(1,1)*L(1,5)
2174 IF TR$(J)=>"A" THEN 2176
2175 IF H(1)=0 AND E(1)<11 THEN TR$(J)="SLIP"
2176 IF TR$(J)="SLIP" THEN GOSUB 6000
2180 GOTO 1250
2190 PRINT "CAR #1 OVERREVED AT ";H(1);"RPM";X(1);" FT.";S(1)*3600/5280;" MPH"
2200 P(1,1)=0
2210 GOTO 1250
2220 IF TR$(J)="TH" THEN GOSUB 6100
2221 IF TR$(J)="HYD" THEN GOSUB 6300
2224 IF R=0 THEN 2300
2230 IF TR$(2)="PG" THEN GOSUB 6200
2232 IF TR$(J)="TH1" THEN GOSUB 6100
2235 IF TR$(J)="SLIP" THEN GOSUB 6000
2240 IF H(2)<P(2,2)*1.0001 THEN 1250
2250 IF N(2)>1 THEN 2275
2260 IF H(2)>(P(2,2)*1.07) THEN 2350
2270 IF N(2)=1 THEN 1250
2275 IF X(1)>1320 THEN 1780
2280 IF J=2 THEN PRINT USING"           #### FEET          ||     ##.##     ||SHIFTING - #### RPM, ### MPH ";X(1);T+(T1/10);H(J);S(J)*3600/5280
2290 N(2)=N(2)-1
2300 IF N(2)=4 THEN E(2)=L(2,4)*L(2,5)
2310 IF N(2)=3 THEN E(2)=L(2,3)*L(2,5)
2320 IF N(2)=2 THEN E(2)=L(2,2)*L(2,5)
2330 IF N(2)=1 THEN E(2)=L(2,1)*L(2,5)
2335 IF H(2)=0 AND E(2)<11 THEN TR$(J)="SLIP"
2336 IF TR$(J)="SLIP" THEN GOSUB 6000
2340 GOTO 1250
2350 PRINT "CAR #2 OVERREVED AT ";H(2);"RPM";X(2);" FT.";S(2)*3600/5280;" MPH"
2360 PRINT "CAR #2 -- GEAR =";N(2);" FINAL="; E(2);"  J=";J
2370 P(2,1)=0
2380 GOTO 1250
2390 IF Q(J)<>0 THEN 1380
2400 IF Q(J)<>0 THEN 1380
2410 BW1=L(J,N(J))*L(J,5)
2420 IF BW1>11 THEN 2500
2430 BW2=14*(P(J,1)+P(J,3)):BW3=P(J,1)/P(J,3):BW4=P(J,3)/(O(J,1)*.085)
2440 IF BW3>BW4 THEN 2470
2450 IF X(J)>(50-(O(J,1)/100)) THEN 2470 ELSE BURN1=BW2*BW4
2460 IF T>2 THEN 2470 ELSE 2480
2470 BURN1=BW2*BW3
2480 REM
2490 GOTO 2510
2499 REM:WAS BURN1=(P(J,1)+P(J,3))*(L(J,N(J))*L(J,5))
2500 BURN1=(P(J,1)+P(J,3))*11
2510 BURN2=(BURN1*2.5)/D(J)
2520 BURN3=(BURN2-O(J,1))/100
2530 BURN4=BURN3-W(J)
2540 IF TR$(J)="PG" THEN 2560
2550 GOTO 2570
2560 BURN4=(BURN3-W(J))/3
2570 IF BURN4<S(J) THEN 1360
2580 GOTO 1400
2590 REM: NEW POWER & ACCEL ROUTINE
2600 REM: GO TO 1610 FOR SPEED COMPUTE AFTER THIS RTN
2610 IF H(J)>(1000/P(J,7)) THEN 2640
2620 P(J,8)=((1000/P(J,7))*P(J,6))+((1000/P(J,5))*P(J,1))
2630 GOTO 2800
2640 REM:
2650 IF H(J)>P(J,7) THEN 2700
2660 P(J,8)=((H(J)/P(J,7))*P(J,6))+((H(J)/P(J,5))*P(J,1))
2670 IF TR$(J)="PG" THEN 2800
2680 IF P(J,8)<P(J,3)*1.1 THEN P(J,8)=P(J,3)*1.1
2690 GOTO 2800
2700 REM:
2710 IF H(J)>P(J,5) THEN 2750
2720 PT1=P(J,6)/P(J,3)
2721 IF PT1>.95 THEN PT1=.95
2722 IF PT1<.8 THEN PT1=.8
2723 PT2=1-PT1
2725 P(J,8)=P(J,6)-((P(J,6)*PT2)*(H(J)/P(J,5)))
2730 P(J,8)=P(J,8)+(((H(J)/P(J,5))*(H(J)/P(J,5)))*P(J,1))
2740 GOTO 2800
2750 REM:
2760 P(J,8)=P(J,6)-((P(J,6)*((H(J)-P(J,7))/P(J,7)))*((P(J,1)/P(J,3)*(H(J)/(P(J,7)*1.2)))))
2770 P(J,8)=P(J,8)+(P(J,1)-((P(J,1)*((H(J)-P(J,5))/P(J,5))))*((P(J,1)/P(J,3))*.99))
2780 REM:IF H(J)>P(J,5)-100 THEN PRINT USING "9272 - J = # -- RPM = #### -- P(J,8) = ### ";J;H(J);P(J,8)
2800 REM:PRINT USING "9500 - LANE #  RPM = #### -- P(J,8) = #### -- CID = ###";J;H(J);P(J,8);P(J,3)
2810 IF S(J)>66 THEN WR=.24+(S(J)/1000) ELSE 2880
2820 IF S(J)>99 THEN WR=.45+(S(J)/1000)
2830 IF S(J)>119 THEN WR=.55+(S(J)/1000)
2840 IF S(J)>138 THEN WR=.65+(S(J)/1000)
2850 IF S(J)>147 THEN WR=.75+(S(J)/1000)
2860 IF S(J)>157 THEN WR=.85+(S(J)/1000)
2870 IF S(J)>169 THEN WR=.95+(S(J)/1000)
2880 IF S(J)<=66 THEN WR=.12+(S(J)/1000)
2890 IF P(J,8)>P(J,3)*1.5 THEN 2910
2900 GOTO 2930
2910 A=((P(J,8)*1.33)*(E(J))*(2.2/D(J)))/((O(J,1)/8)+(S(J)*WR))
2911 REM:A=((P(J,8)*1.33)*E(J))/((O(J,1)/8)+(S(J)*WR))
2920 GOTO 2940
2930 A=((P(J,8)+(P(J,3)/2))*(E(J)*(2.2/D(J))))/((O(J,1)/8)+(S(J)*WR))
2940 REM:
2950 S(J)=S(J)+A*(.099-(S(J)*.00028))
2960 REM:IF H(J)>P(J,5)-100 THEN PRINT USING "9555 - J = # -- RPM = #### -- P(J,8) = ### -- A =###.## -- RAT = ##.##";J;H(J);P(J,8);A;E(J)
2970 GOTO 1550
3160 RETURN
3170 PRINT USING "HP = ###  @ ####  TORQUE = ### @ ####  DISP = ### ";P(Z11,1);P(Z11,5);P(Z11,6);P(Z11,7);P(Z11,3)
3180 REM:PRINT USING "PROGRAM RECOMMENDED MAX RPM IS ####";P(Z11,5)*(1.05+(P(Z11,1)/1000))
3190 RETURN
6000 REM:SLIP RTN
6020 IF O(J,2)=0 THEN O(J,2)=L(J,N(J))
6030 IF H(J)=0 THEN L(J,N(J))=11/L(J,5) ELSE 6050
6035 E(J)=L(J,N(J))*L(J,5)
6040 IF H(J)=0 THEN RETURN
6050 IF H(J)<P(J,7)/3 THEN L(J,N(J))=L(J,N(J))-.003:GOTO 6065
6055 IF H(J)<P(J,7)-100 THEN L(J,N(J))=L(J,N(J))-.02:GOTO 6065
6060 IF H(J)<(P(J,7)+P(J,5))/2 THEN L(J,N(J))=L(J,N(J))-.04 ELSE L(J,N(J))=L(J,N(J))-.06
6065 REM:PRINT USING" 6065 - J = #; RPM = ####; N=#; L=##.##; E=##.##";J;H(J);N(J);L(J,N(J));E(J)
6070 IF L(J,N(J))<O(J,2)+.06 THEN L(J,N(J))=O(J,2):TR$(J)=" "
6090 E(J)=L(J,N(J))*L(J,5)
6095 RETURN
6100 REM:TURBO HYDRO
6110 IF N(J)<3 THEN RETURN
6120 IF TR$(J)="TH1" THEN 6140
6125 TR$(J)="TH1"
6130 L(J,3)=5.25
6135 RETURN
6140 L(J,3)=L(J,3)-(H(J)/10000)
6145 IF L(J,3)>2.5 THEN 6160
6150 TR$(J)="TH2"
6155 L(J,3)=2.5
6160 E(J)=L(J,N(J))*L(J,5)
6165 RETURN
6200 REM: PG REVISION
6210 IF N(J)=1 THEN 6250
6220 IF L(J,2)=1.89 THEN 6290
6221 IF H(J)>1200 THEN 6225
6222 L(J,2)=3.82
6224 GOTO 6290
6225 L(J,2)=1.82+(2-(2*(H(J)/((P(J,7)+5500)/2))))
6230 IF L(J,2)>1.89 THEN 6290
6235 L(J,2)=1.89
6237 L(J,1)=1
6240 GOTO 6290
6250 IF L(J,1)=1.05 THEN 6290
6255 IF L(J,1)=1 THEN L(J,1)=1.18
6260 L(J,1)=L(J,1)-(H(J)/120000!)
6270 IF L(J,1)<1.05 THEN L(J,1)=1.05
6290 E(J)=L(J,N(J))*L(J,5)
6292 REM:prinT USING "6292 - RPM = #### -- GR= #; ##.##; ##.##";H(J);N(J);L(J,N(J));E(J)
6295 RETURN
6300 REM: HYD
6310 IF O(J,2)=1 THEN RETURN
6315 IF O(J,2)=4 THEN 6335
6320 O(J,3)=P(J,2)
6325 IF L(J,3)>2.5 THEN P(J,2)=P(J,7)*.8 ELSE P(J,2)=P(J,7)*1.19
6328 O(J,2)=4
6330 RETURN
6335 IF N(J)=3 THEN P(J,2)=O(J,3) ELSE RETURN
6340 O(J,2)=1
6345 RETURN
8900 CLS
8904 KEY OFF
8905 CLS
8910 LOCATE 25:PRINT USING "WEIGHT= #### - DISP= ### - HP= ### @ #### - TORQUE= ### @ #### - REAR= #.##";O(L,1);P(L,3);P(L,1);P(L,5);P(L,6);P(L,7);L(L,5)
8920 RETURN
9800 PRINT USING "HP = ###  @ ####  TORQUE = ### @ ####  DISP = ### ";P(Z11,1);P(Z11,5);P(Z11,6);P(Z11,7);P(Z11,3)
9820 PRINT USING "PROGRAM RECOMMENDED MAX RPM IS ####";P(Z11,5)*(1.05+(P(Z11,1)/1000))
9830 RETURN
10000 REM: CHEV TRANSMISSION RATIOS
10010 PRINT "       PLEASE INPUT THE 1ST GEAR RATIO OF THE TRANSMISSION OF YOUR CHOICE":INPUT L(L,4)
10020 IF L(L,4)=2.94 THEN N(L)=3:L(L,2)=1.68:L(L,3)=2.94
10030 IF L(L,4)=3.06 THEN N(L)=4:L(L,3)=1.63:L(L,2)=1.05:L(L,1)=1:O(L,1)=O(L,1)+150
10040 IF L(L,4)=2.85 THEN N(L)=4:L(L,2)=1.35:L(L,3)=2.02:O(L,1)=O(L,1)+25
10050 IF L(L,4)=2.47 THEN N(L)=3:L(L,2)=1.53:L(L,3)=2.47
10052 IF L(L,4)=2.58 THEN N(L)=3:L(L,2)=1.48:L(L,3)=2.58
10060 IF L(L,4)=3.62 THEN N(L)=2:L(L,2)=1.1:L(L,2)=3.62:O(L,1)=O(L,1)+95:TR$(L)="PG"
10062 IF L(L,4)=3.96 THEN N(L)=4:L(L,2)=1.53:L(L,3)=2.63:O(L,1)=O(L,1)+198:TR$(L)="HYD"
10063 IF L(L,4)=3.97 THEN N(L)=4:L(L,2)=1.33:L(L,3)=2.23:O(L,1)=O(L,1)+198:TR$(L)="HYD"
10064 IF L(L,4)=2.5 THEN N(L)=3:L(L,2)=1.55:L(L,3)=2.5:O(L,1)=O(L,1)+125:TR$(L)="TH"
10070 IF L(L,4)=2.21 THEN N(L)=3:L(L,2)=1.33:L(L,3)=2.21
10080 IF L(L,4)=2.2 THEN N(L)=4:L(L,2)=1.31:L(L,3)=1.64:O(L,1)=O(L,1)+25
10090 IF L(L,4)=2.54 THEN N(L)=4:L(L,2)=1.66:L(L,3)=1.91:O(L,1)=O(L,1)+25
10092 IF L(L,4)=2.56 THEN N(L)=4:L(L,2)=1.48:L(L,3)=1.91:O(L,1)=O(L,1)+25
10100 IF L(L,4)=2.65 THEN N(L)=3:L(L,1)=1:L(L,2)=1.51:L(L,3)=2.65
10110 IF L(L,4)=2.39 THEN L(L,3)=2.39:L(L,2)=1.53:N(L)=3
10190 IF L(L,2)=0 THEN 10010
10192 IF L(L,1)=0 THEN L(L,1)=1
10195 RETURN
10200 REM: GET ENGINE DATA
10210 SM$=MFR$(PL)
10225 IF P(L,3)=999 THEN SM$(L)="":GOTO 10210
10227 IF P(L,1)>0 THEN 10235
10230 INPUT "HORSEPOWER";P(L,1)
10235 SP1=P(L,1):SP3=P(L,3)
10300 CLOSE #1
10301 OPEN "I",#1,"ENGINEX3"
10380 WHILE NOT EOF(1)
10390 INPUT#1,MFGR$,P1,P2,P3,P4,P5,P6,P7,NC,NB,CR,CAM$,BRTH
10400 IF SM$="LIST" THEN 10450
10410 IF SM$=MFGR$ THEN 10420 ELSE 10500
10420 IF SP3=P3 THEN 10430 ELSE 10500
10430 IF SP1=P1 THEN 10440 ELSE 10500
10440 P(L,1)=P1:P(L,2)=P2:P(L,3)=P3:P(L,4)=P4:P(L,5)=P5:P(L,6)=P6:P(L,7)=P7
10442 NC(L)=NC:NB(L)=NB:CR(L)=CR:CAM$(L)=CAM$:BRTH(L)=BRTH
10450 PRINT USING "MFGR &, DISP=###, HP=### @ ####, TORQUE=### @ ####";MFGR$;P3;P1;P5;P6;P7
10452 IF P(L,5)=0 THEN 10460
10453 CL=CL+1
10455 CLOSE#1
10457 RETURN
10460 LL=LL+1
10470 IF LL<23 THEN 10500
10480 INPUT "PRESS ENTER FOR MORE";LX$
10482 LL=1
10500 WEND
10502 CLOSE#1
10510 IF P(L,5)>0 THEN RETURN
10520 PRINT "YOU CHOSE AN INELLIGIBLE HP/DISPLACEMENT COMBINATION - TRY AGAIN (PRESS ENTER)
10530 INPUT I$
10535 P(L,1)=0:P(L,3)=0
10540 GOTO 10200
11000 GOSUB 11500
11005 RVL=10
11010 IF ROUND=1 THEN 15000
11020 IF ROUND=2 THEN 16000
11025 IF LWINT<LOT THEN RVL=RVL+10
11030 IF ROUND=3 THEN 17000
11064 OPEN "O",#1,"WINNERS1.DG1"
11066 WRITE #1,1,LOT
11067 WRITE #1,2,HIS
11068 WRITE #1,3,PV(1)
11070 WRITE #1,4,PV(2)
11072 IF SWT18>0 THEN WRITE #1,18,SWT18 ELSE 11084
11074 IF SWT17>0 THEN WRITE #1,17,SWT17 ELSE 11084
11076 IF SWT16>0 THEN WRITE #1,16,SWT16 ELSE 11084
11078 IF SWT15>0 THEN WRITE #1,15,SWT15 ELSE 11084
11080 IF SWT14>0 THEN WRITE #1,14,SWT14 ELSE 11084
11082 IF SWT13>0 THEN WRITE #1,13,SWT13 ELSE 11084
11084 CLOSE #1
11085 OPEN "O",#1,"P1-END.DG1"
11090 WRITE #1,N$(1),VL(1),SV(1),DSCR$(1),O(1,1),W(1),D(1),P(1,1),P(1,3),WIN1,N(1),L(1,1),L(1,2),L(1,3),L(1,4),L(1,5),MFR$(1),VLCD(1),YR(1),TR$(1)
11092 WRITE #1,N$(2),VL(2),SV(2),DSCR$(2),O(2,1),W(2),D(2),P(2,1),P(2,3),WIN2,N(2),L(2,1),L(2,2),L(2,3),L(2,4),L(2,5),MFR$(2),VLCD(2),YR(2),TR$(2)
11095 CLOSE #1
11096 INPUT "END OF PHASE 1 - PRESS <ENTER> TO LOAD PHASE 2 OR 'E' TO END";I$
11097 IF I$="E" THEN END ELSE RUN "DRAG-P2"
11098 CLOSE #1
11099 END
11100 PRINT N$(PL)
11110 PRINT USING "  YOU HAVE $ #### IN SAVINGS; THE CAR IS NOW VALUED AT $ ####";SV(PL);VL(PL)
11120 PRINT USING "  YOUR NET WORTH IS $ ####";SV(PL)+VL(PL)
11130 PRINT:PRINT:INPUT "PRESS ENTER TO CONTINUE";I$
11140 RETURN
11200 STOP
11205 REM - THIS USED TO BE THE INCOME ROUTINE
11300 INPUT#1,NOX,P1,P3,PR,DESC1$,DESC2$,DESC3$,O,D,W,N,L4,L3,L2,L1,L5,MFGR$,VLCX,YRX,T$
11305 CLS:LOCATE 25:PRINT USING "& -- SAVINGS = $ #### ";N$(PL);SV(PL)
11310 PRINT DESC1$
11320 PRINT USING "NUMBER #, LISTED AT  ### HP, ### CID, WILL COST YOU $ ####";NOX;P1;P3;PR
11330 PRINT DESC2$
11340 PRINT DESC3$
11350 INPUT "PRESS ENTER TO CONTINUE";I$
11360 RETURN
11400 INPUT#1,NOX,P1,P3,PR,DESC1$,DESC2$,DESC3$,O,D,W,N,L4,L3,L2,L1,L5,MFGR$,VLCX,YRX,T$
11410 IF CN(PL)=NOX THEN 11415 ELSE RETURN
11415 IF SV(PL)<PR THEN 11465
11420 P(PL,1)=P1:P(PL,3)=P3:O(PL,1)=O:D(PL)=D:W(PL)=W:N(PL)=N
11425 NTR(PL)=N(PL):DSCR$(PL)=DESC1$
11430 L(PL,4)=L4:L(PL,3)=L3:L(PL,2)=L2:L(PL,1)=L1:L(PL,5)=L5
11432 VLCD(PL)=VLCX:YR(PL)=YRX
11435 SV(PL)=SV(PL)-PR
11440 VL(PL)=PR
11450 MFR$(PL)=MFGR$:NTR(PL)=N(PL):TR$(PL)=T$
11460 RETURN
11465 IF YRX<FINYR THEN 11470 ELSE 11420
11470 PRINT:PRINT:PRINT:PRINT
11472 PRINT "THE CAR YOU HAVE CHOSEN IS TOO OLD TO BE FINANCED AND YOU CANNOT"
11475 INPUT "AFFORD TO PAY CASH FOR IT.  BETTER LUCK NEXT TIME.  PRESS ENTER TO CONTINUE";I$
11478 SV(PL)=SV(PL)-WORK1:VL(PL)=WORK1
11480 RETURN
11500 GOSUB 11900
11501 IF SWT18>0 THEN PRINT USING "FIRST PERSON UNDER 18 SECONDS IS &";N$(SWT18)
11502 IF SWT17>0 THEN PRINT USING "FIRST PERSON UNDER 17 SECONDS IS &";N$(SWT17)
11504 IF SWT16>0 THEN PRINT USING "FIRST PERSON UNDER 16 SECONDS IS &";N$(SWT16)
11505 IF SWT15>0 THEN PRINT USING "FIRST PERSON UNDER 15 SECONDS IS &";N$(SWT15)
11507 IF SWT14>0 THEN PRINT USING "FIRST PERSON UNDER 14 SECONDS IS &";N$(SWT14)
11508 IF SWT13>0 THEN PRINT USING "FIRST PERSON UNDER 13 SECONDS IS &";N$(SWT13)
11509 IF SWT12>0 THEN PRINT USING "FIRST PERSON UNDER 12 SECONDS IS &";N$(SWT12)
11510 PRINT:PRINT
11512 IF WS>HIS THEN HIS=WS:GOTO 11515:PV(LWIN)=PV(LWIN)+.5
11513 IF LWINT<LOT THEN LOT=LWINT:GOTO 11515:PV(LWIN)=PV(LWIN)+.5
11514 GOTO 11522
11515 VL(LWIN)=VL(LWIN)+RVL
11516 PV(LWIN)=PV(LWIN)+1
11522 IF HIS>HS(L) THEN HS(L)=HIS:GOTO 11525
11523 IF LOT<LT(L) THEN LT(L)=LOT:GOTO 11525
11524 GOTO 11528
11525 VL(LWIN)=VL(LWIN)+RVL
11528 PRINT USING "END OF RACE ##";ROUND
11530 PRINT USING " & , YOU ARE CURRENTLY DRIVING A  ### HP & ";N$(1);P(1,1);DSCR$(1)
11540 PRINT USING "  YOU HAVE $ ##### IN SAVINGS; THE CAR IS NOW VALUED AT $ ####";SV(1);VL(1)
11545 PRINT USING "  YOUR NET WORTH IS $ ##### AND YOU HAVE WON ## RACES";SV(1)+VL(1);WIN1
11550 PRINT:PRINT
11560 PRINT USING " & , YOU ARE CURRENTLY DRIVING A  ### HP & ";N$(2);P(2,1);DSCR$(2)
11570 PRINT USING "  YOU HAVE $ ##### IN SAVINGS; THE CAR IS NOW VALUED AT $ ####";SV(2);VL(2)
11575 PRINT USING "  YOUR NET WORTH IS $ ##### AND YOU HAVE WON ## RACES";SV(2)+VL(2);WIN2
11580 PRINT:PRINT:INPUT "PRESS ENTER TO CONTINUE";I$
11590 RETURN
11700 IF W(PL)>6.99 THEN RETURN
11702 IF SV(PL)<-100 THEN RETURN
11705 PRINT "YOU WILL HAVE THE OPTION OF CHANGING YOUR REAR END RATIO AND YOU CAN BUY A      PAIR OF 'CHEATER SLICKS'."
11710 PRINT USING " &, YOU CAN BUY CHEATER SLICKS FOR $100";N$(PL)
11720 INPUT "DO YOU WANT TO BUY THEM NOW";I$
11730 IF I$="N" THEN 11800
11740 W(L)=7:SV(L)=SV(L)-100
11750 PRINT USING "&, YOU HAVE CHEATER SLICKS AND A $ #### TO SPEND";N$(PL);SV(PL)
11760 IF JOB$(PL)="B" THEN 11780
11770 IF JOB$(PL)="D" THEN 11780 ELSE 11790
11780 MD=60:GOTO 11800
11790 MD=125
11800 MD=125
11802 IF SV(PL)<0 THEN RETURN
11805 PRINT USING "&, YOU HAVE $ #### AND A #.## RATIO; ANOTHER RATIO WILL COST BETWEEN $ ### AND $ ###.";N$(PL);SV(PL);L(PL,5);MD;MD*1.4
11810 INPUT "DO YOU WANT TO CHANGE REAR END RATIOS";I$
11820 IF I$="N" THEN RETURN
11830 INPUT "WHAT IS THE REAR END RATIO YOU WOULD LIKE";L5W
11840 IF L5W<4.69 THEN 11892
11850 PRINT USING "##.## IS PRETTY HIGH FOR STREET USE - DO YOU WANT TO TRY ANOTHER?(Y/N)";L5W
11860 INPUT I$
11870 IF I$="Y" THEN 11800
11880 VL(PL)=VL(PL)-50
11890 MD=MD*1.3
11892 SV(PL)=SV(PL)-MD
11895 L(PL,5)=L5W
11899 RETURN
11900 IF LOT=0 THEN LOT=99.99
11901 IF TSAV=0 THEN TSAV=99.99
11902 IF LWINT<LOT THEN LOT=LWINT
11905 IF LWINT<TSAV THEN TSAV=LWINT ELSE RETURN
11910 IF SWT18>0 THEN 11915
11912 IF LWINT<18 THEN SWT18=LWIN:VL(LWIN)=VL(LWIN)+50:PV(LWIN)=PV(LWIN)+.5
11915 IF SWT17>0 THEN 11925
11920 IF LWINT<17 THEN SWT17=LWIN:VL(LWIN)=VL(LWIN)+75:RVL=RVL+25:PV(LWIN)=PV(LWIN)+1
11925 IF SWT16>0 THEN 11935
11930 IF LWINT<16 THEN SWT16=LWIN:VL(LWIN)=VL(LWIN)+75:RVL=RVL+25:PV(LWIN)=PV(LWIN)+1.3
11935 IF SWT15>0 THEN 11950
11940 IF LWINT<15 THEN SWT15=LWIN:VL(LWIN)=VL(LWIN)+100:RVL=RVL+50:PV(LWIN)=PV(LWIN)+1.8
11945 IF LWINT>14 THEN RETURN
11950 IF SWT14>0 THEN 11970
11955 IF LWINT<14 THEN SWT14=LWIN:VL(LWIN)=VL(LWIN)+125:RVL=RVL+100:PV(LWIN)=PV(LWIN)+2.1
11960 IF LWINT>13 THEN RETURN
11970 IF SWT13>0 THEN 11990
11975 IF LWINT<13 THEN SWT13=LWIN:VL(LWIN)=VL(LWIN)+150:RVL=RVL+150:PV(LWIN)=PV(LWIN)+3
11980 IF LWINT>12 THEN RETURN
11990 IF SWT12>0 THEN 11995
11992 IF LWINT<12 THEN SWT12=LWIN:VL(LWIN)=VL(LWIN)+200:RVL=RVL+200:PV(LWIN)=PV(LWIN)+4
11995 RETURN
12000 CLS
12003 KEY OFF
12005 LOCATE 25:PRINT USING "&, You have $ ##### to spend.";N$(L);SV(L)
12010 RETURN
12020 INPUT "PRESS ENTER TO CONTINUE";I$
12025 RETURN
12030 PRINT:PRINT USING "&, you have a ### HP, ### CID & and $#### remaining";N$(L);P(L,1);P(L,3),DSCR$(L),SV(L)
12035 RETURN
12050 PRINT USING "&, YOU HAVE $ #### TO MODIFY YOUR &.";N$(PL);SV(PL);DSCR$(PL)
12055 PRINT USING "MODIFICATIONS WILL COST YOU $ ## PER HORSEPOWER.";MD
12100 REM:  INCREASE VALUE OF JUNKERS
12105 IF ROUND=3 THEN VLX=150 ELSE VLX=100
12110 IF VL(PL)>P(PL,1) THEN RETURN
12120 IF VL(PL)>VLX THEN RETURN
12130 IF P(PL,1)>100 THEN VL(PL)=VL(PL)*2 ELSE VL(PL)=P(PL,1)
12140 RETURN
12200 IF MFR$(L)="MOPAR" THEN 12205
12204 RETURN
12205 IF N(L)<4 THEN 12250
12210 PRINT USING "&, you have blown first gear on the old 4-speed.";N$(L)
12215 PRINT "Your options are to avoid first gear or buy the available 3-speed."
12218 INPUT "Another transmission will cost $50.  Enter 'B' to buy";I$
12220 IF I$="B" THEN 12225
12221 N(L)=3:L(L,4)=0
12222 IF P(L,1)>135 THEN 12270 ELSE RETURN
12225 L(L,4)=0:L(L,3)=2.67:L(L,2)=1.55:N(L)=3
12226 SV(L)=SV(L)-50
12230 IF P(L,1)>135 THEN 12270 ELSE RETURN
12250 IF L(L,3)<2.5 THEN 12255
12252 IF P(L,1)>135 THEN 12270 ELSE RETURN
12255 PRINT USING "&, your clutch is gone.  It will cost you $50 to replace it.";N$(L)
12260 INPUT "You have no alternative; press enter to continue";I$
12265 SV(L)=SV(L)-50
12267 IF P(L,1)<136 THEN RETURN
12270 IF P(L,5)<3500 THEN RETURN
12275 IF P(L,2)<4100 THEN RETURN
12280 PRINT USING "&, you have pushed the old straight 8 beyond its limits.";N$(L)
12282 PRINT "It is going to blow up soon.  Perhaps you should consider selling."
12284 INPUT "Press enter to continue";I$
12286 IF ROUND=2 THEN P(L,1)=2 ELSE P(L,1)=135
12290 RETURN
12400 IF ROUND=3 THEN 12500
12410 IF VLCD(PL)>4 THEN 12450
12420 IF MFR$(PL)="FF" THEN 12450
12430 IF YR(PL)<46 THEN VLPCT=.6 ELSE VLPCT=.9
12440 RETURN
12450 IF YR(PL)<46 THEN VLPCT=.8 ELSE VLPCT=.9
12460 IF VLCD(PL)>6 THEN VLPCT=VLPCT+.1
12470 IF VLCD(PL)>7 THEN VLPCT=VLPCT+.1
12480 IF VLCD(PL)>8 THEN VLPCT=VLPCT+.1
12490 RETURN
12500 IF VLCD(PL)>4 THEN 12550
12510 IF MFR$(PL)="FF" THEN 12550
12520 IF YR(PL)>48 THEN VLPCT=.9
12530 IF YR(PL)<49 THEN VLPCT=.6
12540 IF YR(PL)<46 THEN VLPCT=.3
12545 GOTO 12560
12550 VLPCT=.9
12560 IF VLCD(PL)<4 THEN VLPCT=VLPCT-.1
12565 IF VLCD(PL)>6 THEN VLPCT=VLPCT+.1
12570 IF VLCD(PL)>7 THEN VLPCT=VLPCT+.1
12580 IF VLCD(PL)>8 THEN VLPCT=VLPCT+.1
12590 RETURN
12900 PRINT:PRINT
12903 PRINT "You will have to help the computer choose a car by supplying a 'seed' number."
12905 PRINT
12907 IF TUT$="Y"THEN GOSUB 20050
12910 RANDOMIZE
12920 CN(L)=INT(RND*10)
12930 IF CN(L)=0 THEN CN(L)=10
12990 GOTO 14400
14000 REM
14100 FOR L=1 TO 2
14105 PL=L
14108 SV(L)=250
14110 CLS:PRINT USING "Player in lane #, what is your name?";L
14115 IF TUT$="Y" THEN PRINT "TYPE YOUR NAME AND PRESS THE ENTER KEY"
14120 INPUT N$(L)
14130 PRINT:PRINT USING "Hello &, you will start with $250.  You can choose a car from";N$(L)
14131 PRINT "the back row of a disreputable used car lot (prices less than $200).  Or you    can let the computer choose a car for you from the front row of a junk yard.    Junk yard prices are less than $50."
14132 IF TUT$="Y" THEN GOSUB 20000
14133 INPUT "Press <ENTER> to choose for yourself or C<ENTER> to have the computer choose.";I$
14134 IF I$="C" THEN 12900
14140 INPUT "PRESS ENTER TO LOOK AT THE CARS";I$
14142 IF TUT$="Y" THEN GOSUB 20100
14145 GOSUB 12000
14150 IF L=1 THEN OPEN "I",#1,"USED50-A.DG1"
14155 IF L=2 THEN OPEN "I",#1,"USED50-B.DG1"
14160 WHILE NOT EOF(1)
14170 GOSUB 11300
14180 WEND
14190 CLOSE #1
14200 PRINT:INPUT "INPUT THE NUMBER OF THE CAR YOU WANT";CN(L)
14205 IF TUT$="Y" THEN GOSUB 20150
14210 IF L=1 THEN OPEN "I",#1,"USED50-A.DG1"
14215 IF L=2 THEN OPEN "I",#1,"USED50-B.DG1"
14220 WHILE NOT EOF(1)
14230 GOSUB 11400
14240 WEND
14250 CLOSE #1
14255 GOSUB 10200
14260 GOSUB 12030
14270 GOSUB 12020
14280 NEXT L
14290 GOTO 890
14400 IF L=1 THEN OPEN "I",#1,"JUNK2.DG1"
14410 IF L=2 THEN OPEN "I",#1,"JUNK1.DG1"
14420 WHILE NOT EOF(1)
14430 GOSUB 11400
14440 WEND
14445 CLOSE #1
14446 GOSUB 12030
14450 GOTO 14255
15000 PRINT USING "Congratulations, &, you have won the first race.";N$(LWIN)
15010 PRINT "Considering where you both bought your cars, both of them could probably use    some work."
15020 GOSUB 12020
15030 FOR PL=1 TO 2
15040 MD=5
15050 SV(PL)=SV(PL)+100
15060 L=PL
15065 IF P(L,3)=323 THEN GOSUB 12200
15070 GOSUB 12000
15080 PRINT:PRINT:PRINT N$(PL)
15090 INPUT "YOU CAN MODIFY OR REBUILD YOUR ENGINE (M), OR RACE IT AS IS (R)";I$
15100 IF I$="R" THEN 15510
15110 PRINT USING "\        \ YOU HAVE $ #### TO MODIFY YOUR & - COST IS $ ## PER HP.";N$(PL);SV(PL);DSCR$(PL);MD
15120 INPUT "ENTER 'N' TO SKIP MODIFICATIONS THIS TIME";I$
15130 PRINT USING "& ,YOU HAVE ### HP AND $ ####";N$(PL);P(PL,1);SV(PL)
15140 IF I$="N" THEN 15510
15150 OPEN "I",#1,"ENGINEX3"
15160 SM$=MFR$(PL)
15170 IF SM$="" THEN INPUT "MANUFACTURER";SM$
15180 SP3=P(PL,3)
15190 SP1=0
15200 WHILE NOT EOF(1)
15210 INPUT#1,MFGR$,P1,P2,P3,P4,P5,P6,P7,NC,NB,CR,CAM$,BRTH
15220 IF SM$=MFGR$ THEN 15230 ELSE 15320
15230 IF SP3>0 THEN 15240 ELSE 15270
15240 IF SP3=P3 THEN 15250 ELSE 15320
15250 IF SP1>0 THEN 15260 ELSE 15270
15260 IF SP1=P1 THEN 15270 ELSE 15320
15270 PRINT USING "MFGR &, DISP=###, HP=### @ ####, TORQUE=### @ ####";MFGR$;P3;P1;P5;P6;P7
15280 LL=LL+1
15290 IF LL<23 THEN 15320
15300 INPUT "PRESS ENTER FOR MORE";LX$
15310 LL=1
15320 WEND
15330 CLOSE #1
15340 INPUT "WHAT IS THE EXACT HORSEPOWER YOU WANT";WP1
15345 IF WP1=0 THEN 15420
15350 WORK1=WP1-P(PL,1)
15360 WORK2=WORK1*MD
15370 WORK3=SV(PL)-WORK2
15380 IF WORK3<0 THEN PRINT "YOU CAN'T AFFORD THAT - TRY AGAIN":GOTO 15340
15390 PRINT USING "YOU CAN INCREASE TO ### HP - IT WILL LEAVE YOU WITH $ ####";WP1;WORK3
15400 INPUT "ENTER 'Y' TO CONFIRM MODIFICATION";I$
15410 IF I$="Y" THEN 15430
15420 PRINT "YOU OPTED NOT TO MODIFY AT THIS TIME":GOTO 15500
15430 P(PL,1)=WP1:SV(PL)=WORK3:VL(PL)=VL(PL)+(WORK1*2)
15440 PRINT:PRINT USING "MODIFICATIONS COMPLETE; HP = ###, CASH = $ ####, CAR IS WORTH $ ####";P(PL,1);SV(PL);VL(PL)
15450 L=PL
15460 P(L,2)=0:P(L,5)=0:P(L,6)=0:P(L,7)=0
15470 GOSUB 10200
15480 GOSUB 8900
15490 INPUT "PRESS ENTER TO CONTINUE";I$
15500 CLOSE #1
15510 REM
15520 NEXT PL
15530 GOTO 890
16000 REM
16010 FOR PL=1 TO 2
16012 CLS
16014 FINYR=54
16015 MD=5
16020 SV(PL)=SV(PL)+100
16025 L=PL
16028 GOSUB 12000
16030 IF P(L,3)<229 THEN 16040
16033 IF P(L,3)=323 THEN GOSUB 12200
16035 IF P(L,1)<95 THEN 16060 ELSE 16045
16040 IF P(L,3)>207 THEN 16042
16041 IF P(L,1)<74 THEN 16060 ELSE 16045
16042 IF P(L,1)<78 THEN 16060
16045 GOSUB 12100
16050 GOTO 16130
16060 PRINT USING "&, you have blown your engine.  You knew when you bought it that it";N$(L)
16062 PRINT "needed work.  Now you have no choice."
16065 P(L,1)=0
16070 GOSUB 12030
16080 INPUT "YOU CAN LOOK AT USED CARS (L) OR MODIFY/REBUILD (M)";I$
16090 GOTO 16140
16125 GOTO 16220
16130 PRINT:PRINT:PRINT USING "&, you now have $ ####.";N$(PL);SV(PL)
16131 INPUT "YOU CAN LOOK AT USED CARS (L), MODIFY YOUR CAR (M), OR RACE IT AS IS (R)";I$
16135 IF I$="R" THEN 16740
16140 IF I$="M" THEN 16315
16150 GOSUB 12400
16165 WORK1=(VLPCT*VL(PL))+(P(PL,1)*.1)-(O(PL,1)*.005)
16175 PRINT USING "\        \-YOU HAVE BEEN OFFERED $ #### FOR YOUR &";N$(PL);WORK1;DSCR$(PL)
16180 PRINT "YOU FIGURE THIS IS MUCH MORE THAN YOU COULD GET AS A TRADE-IN"
16185 INPUT "IF YOU ARE SERIOUS ABOUT BUYING ANOTHER CAR, YOU'D BETTER SELL IT NOW (Y/N)";I$
16190 IF I$="N" THEN 16131
16195 SV(PL)=SV(PL)+WORK1
16200 VL(PL)=0
16205 PRINT USING "\        \ YOU HAVE $ #### TO SPEND";N$(PL);SV(PL)
16220 PRINT USING "\        \ YOU ARE GOING TO BUY A CAR; YOU HAVE $ ####";N$(PL);SV(PL)
16222 INPUT "CHOOSE FROM DECENT USED CARS (U) OR NEWSPAPER WANT ADS (W)";IP$
16225 IF IP$="W" THEN OPEN "I",#1,"USED54-B.DG1" ELSE OPEN "I",#1,"USED54-A.DG1"
16235 INPUT "PRESS ENTER TO CONTINUE";I$
16240 WHILE NOT EOF(1)
16245 GOSUB 11300
16250 WEND
16255 CLOSE #1
16275 INPUT "INPUT THE NUMBER OF THE CAR YOU WANT OR '0' TO PASS";CN(PL)
16277 IF CN(PL)>0 THEN 16280
16278 VL(PL)=WORK1:SV(PL)=SV(PL)-WORK1
16279 GOTO 16311
16280 IF IP$="W" THEN OPEN "I",#1,"USED54-B.DG1" ELSE OPEN "I",#1,"USED54-A.DG1"
16295 WHILE NOT EOF(1)
16300 GOSUB 11400
16305 WEND
16307 GOSUB 10200
16310 CLOSE #1
16311 CLS
16312 GOSUB 12030
16315 IF SV(PL)<0 THEN 16740
16318 INPUT "YOU MAY HAVE THE OPPORTUNITY TO MAKE AN ENGINE SWAP-TO LOOK, ENTER (Y/N)";I$
16320 IF I$="N" THEN 16520
16325 OPEN "I",#1,"BOLTIN-Z.DG1":HX=0
16330 WHILE NOT EOF(1)
16335 INPUT #1,MFGR$,OLD,NW,HP,C1,C2,VLOLD
16336 IF C1>ROUND THEN 16365
16340 IF MFGR$=MFR$(PL) THEN 16345 ELSE 16365
16345 IF OLD=P(PL,3) THEN 16350 ELSE 16365
16350 HX=HP
16355 PRINT USING"YOU CAN BUY A ### HP ### FOR $### ";HP;NW;C2
16360 INPUT "PRESS ENTER TO CONTINUE";I$
16365 WEND
16370 CLOSE #1
16375 IF HX=0 THEN 16380 ELSE 16385
16380 PRINT "NO FURTHER ENGINE SWAPS AVAILABLE AT THIS TIME":GOTO 16520
16385 INPUT "INPUT THE HP YOU DESIRE";SP1
16386 IF SP1=0 THEN 16520
16390 OPEN "I",#1,"BOLTIN-Z.DG1":HX=0
16395 WHILE NOT EOF(1)
16400 INPUT #1,MFGR$,OLD,NW,HP,C1,C2,VLOLD
16405 IF MFGR$=MFR$(PL) THEN 16410 ELSE 16425
16410 IF OLD=P(PL,3) THEN 16415 ELSE 16425
16415 IF HP=SP1 THEN 16420 ELSE 16425
16420 NWH=NW:HPH=HP:C1H=C1:C2H=C2:VLHLD=VLOLD
16425 WEND
16430 CLOSE #1
16465 PRINT USING "IT WILL COST YOU $### ; YOU CAN GET SOME HELP INSTALLING IT.";C2H
16470 IF VLHLD=0 THEN VLHLD=P(L,1)*.85
16475 PRINT USING "YOU CAN SELL YOUR OLD ENGINE FOR $###";VLHLD
16480 INPUT "DO YOU WANT TO MAKE THIS SWAP (Y/N)";I$
16485 IF I$="N" THEN 16520
16490 SV(PL)=SV(PL)-C2H+VLHLD
16495 VL(PL)=VL(PL)+(C1H*.65)
16500 P(PL,3)=NWH:P(PL,1)=HPH:P(PL,5)=0:P(PL,6)=0:P(PL,7)=0:P(PL,2)=0:L=PL
16505 GOSUB 10200:GOSUB 8900
16510 PRINT USING "YOU NOW HAVE A ### HP ### CID &";P(PL,1);P(PL,3);DSCR$(PL)
16515 PRINT USING "YOUR SAVINGS IS $#### AND YOUR CAR IS WORTH $####";SV(PL);VL(PL)
16520 REM:MODIFY OLD CAR
16525 IF SV(PL)<0 THEN 16740
16540 PRINT USING "\        \ YOU HAVE $ #### TO MODIFY YOUR & - COST IS $ ## PER HP.";N$(PL);SV(PL);DSCR$(PL);MD
16545 INPUT "ENTER 'N' TO SKIP MODIFICATIONS THIS TIME";I$
16550 PRINT USING "& ,YOU HAVE ### HP AND $ ####";N$(PL);P(PL,1);SV(PL)
16555 IF I$="N" THEN 16740
16560 OPEN "I",#1,"ENGINEX3"
16565 SM$=MFR$(PL)
16570 IF SM$="" THEN INPUT "MANUFACTURER";SM$
16575 SP3=P(PL,3)
16580 SP1=0
16585 WHILE NOT EOF(1)
16590 INPUT#1,MFGR$,P1,P2,P3,P4,P5,P6,P7,NC,NB,CR,CAM$,BRTH
16595 IF SM$=MFGR$ THEN 16600 ELSE 16645
16600 IF SP3>0 THEN 16605 ELSE 16620
16605 IF SP3=P3 THEN 16610 ELSE 16645
16610 IF SP1>0 THEN 16615 ELSE 16620
16615 IF SP1=P1 THEN 16620 ELSE 16645
16620 PRINT USING "MFGR &, DISP=###, HP=### @ ####, TORQUE=### @ ####";MFGR$;P3;P1;P5;P6;P7
16625 LL=LL+1
16630 IF LL<23 THEN 16645
16635 INPUT "PRESS ENTER FOR MORE";LX$
16640 LL=1
16645 WEND
16650 CLOSE #1
16655 INPUT "WHAT IS THE EXACT HORSEPOWER YOU WANT";WP1
16658 IF WP1=0 THEN 16695
16660 WORK1=WP1-P(PL,1)
16665 WORK2=WORK1*MD
16670 WORK3=SV(PL)-WORK2
16675 IF WORK3<0 THEN PRINT "YOU CAN'T AFFORD THAT - TRY AGAIN":GOTO 16655
16680 PRINT USING "YOU CAN INCREASE TO ### HP - IT WILL LEAVE YOU WITH $ ####";WP1;WORK3
16685 INPUT "ENTER 'Y' TO CONFIRM MODIFICATION";I$
16690 IF I$="Y" THEN 16700
16695 PRINT "YOU OPTED NOT TO MODIFY AT THIS TIME":GOTO 16740
16700 P(PL,1)=WP1:SV(PL)=WORK3:VL(PL)=VL(PL)+(WORK1*2)
16705 PRINT:PRINT USING "MODIFICATIONS COMPLETE; HP = ###, CASH = $ ####, CAR IS WORTH $ ####";P(PL,1);SV(PL);VL(PL)
16710 L=PL
16715 P(L,2)=0:P(L,5)=0:P(L,6)=0:P(L,7)=0
16720 GOSUB 10200
16725 GOSUB 8900
16730 GOSUB 12020
16735 CLOSE #1
16740 REM
16750 NEXT PL
16755 GOTO 890
16950 GOTO 16520
17000 REM
17010 FOR PL=1 TO 2
17014 FINYR=54
17015 L=PL
17017 IF P(L,3)=323 THEN GOSUB 12200 ELSE GOSUB 12100
17020 IF MFR$(PL)="FF" THEN MD=5 ELSE MD=6
17021 IF P(L,1)<160 THEN MD=5
17030 SV(PL)=SV(PL)+100
17040 GOSUB 12000
17110 INPUT "YOU CAN LOOK AT USED CARS (L), MODIFY YOUR CAR (M), OR RACE IT AS IS (R)";I$
17120 IF I$="R" THEN 18250
17130 IF I$="M" THEN 17490
17140 GOSUB 12400
17170 WORK1=(VLPCT*VL(PL))+(P(PL,1)*.1)
17200 PRINT USING "\        \-YOU HAVE BEEN OFFERED $ #### FOR YOUR &";N$(PL);WORK1;DSCR$(PL)
17210 PRINT "YOU FIGURE THIS IS MUCH MORE THAN YOU COULD GET AS A TRADE-IN"
17220 INPUT "IF YOU ARE SERIOUS ABOUT BUYING ANOTHER CAR, YOU'D BETTER SELL IT NOW (Y/N)";I$
17230 IF I$="N" THEN 17110
17240 SV(PL)=SV(PL)+WORK1
17250 VL(PL)=0
17290 PRINT USING "\        \ YOU ARE GOING TO BUY A CAR; YOU HAVE $ ####";N$(PL);SV(PL)
17300 IF PL=1 THEN OPEN "I", #1,"USED54-C.DG1"
17310 IF PL=2 THEN OPEN "I",#1,"USED54-D.DG1"
17320 INPUT "PRESS ENTER TO CONTINUE";I$
17330 WHILE NOT EOF(1)
17340 GOSUB 11300
17350 WEND
17360 CLOSE #1
17400 INPUT "INPUT THE NUMBER OF THE CAR YOU WANT OR '0' TO PASS";CN(PL)
17405 IF CN(PL)>0 THEN 17410
17406 SV(PL)=SV(PL)-WORK1:VL(PL)=WORK1
17407 GOTO 17475
17410 IF PL=1 THEN OPEN "I",#1,"USED54-C.DG1"
17420 IF PL=2 THEN OPEN "I",#1,"USED54-D.DG1"
17430 WHILE NOT EOF(1)
17440 GOSUB 11400
17450 WEND
17460 GOSUB 10200
17470 CLOSE #1
17475 CLS
17480 GOSUB 12030
17490 IF SV(PL)<1 THEN 18260
17495 INPUT "YOU MAY HAVE THE OPPORTUNITY TO MAKE AN ENGINE SWAP-TO LOOK, ENTER (Y/N)";I$
17500 IF I$="N" THEN 17840
17510 OPEN "I",#1,"BOLTIN-Z.DG1":HX=0
17520 WHILE NOT EOF(1)
17530 INPUT #1,MFGR$,OLD,NW,HP,C1,C2,VLOLD
17535 IF C1>ROUND THEN 17590
17540 IF MFGR$=MFR$(PL) THEN 17550 ELSE 17590
17550 IF OLD=P(PL,3) THEN 17560 ELSE 17590
17560 HX=HP
17570 PRINT USING"YOU CAN BUY A ### HP ### FOR $### ";HP;NW;C2
17580 INPUT "PRESS ENTER TO CONTINUE";I$
17590 WEND
17600 CLOSE #1
17610 IF HX=0 THEN 17620 ELSE 17630
17620 PRINT "NO FURTHER ENGINE SWAPS AVAILABLE AT THIS TIME":GOTO 17840
17630 INPUT "INPUT THE HP YOU DESIRE";SP1
17635 IF SP1=0 THEN 17840
17640 OPEN "I",#1,"BOLTIN-Z.DG1":HX=0
17650 WHILE NOT EOF(1)
17660 INPUT #1,MFGR$,OLD,NW,HP,C1,C2,VLOLD
17670 IF MFGR$=MFR$(PL) THEN 17680 ELSE 17710
17680 IF OLD=P(PL,3) THEN 17690 ELSE 17710
17690 IF HP=SP1 THEN 17700 ELSE 17710
17700 NWH=NW:HPH=HP:C1H=C1:C2H=C2:VLHLD=VLOLD
17710 WEND
17720 CLOSE #1
17730 PRINT USING "IT WILL COST YOU $### ; YOU CAN GET SOME HELP INSTALLING IT.";C2H
17740 IF VLHLD=0 THEN VLHLD=P(L,1)*.85
17750 PRINT USING "YOU CAN SELL YOUR OLD ENGINE FOR $###";VLHLD
17760 INPUT "DO YOU WANT TO MAKE THIS SWAP (Y/N)";I$
17770 IF I$="N" THEN 17840
17780 SV(PL)=SV(PL)-C2H+VLHLD
17790 VL(PL)=VL(PL)+(C1H*.65)
17800 P(PL,3)=NWH:P(PL,1)=HPH:P(PL,5)=0:P(PL,6)=0:P(PL,7)=0:P(PL,2)=0:L=PL
17810 GOSUB 10200:GOSUB 8900
17820 PRINT USING "YOU NOW HAVE A ### HP ### CID &";P(PL,1);P(PL,3);DSCR$
17830 PRINT USING "YOUR SAVINGS IS $#### AND YOUR CAR IS WORTH $####";SV(PL);VL(PL)
17840 IF SV(PL)<1 THEN 18260
17850 PRINT USING "\        \ YOU HAVE $ #### TO MODIFY YOUR & - COST IS $ ## PER HP.";N$(PL);SV(PL);DSCR$(PL);MD
17860 INPUT "ENTER 'N' TO SKIP MODIFICATIONS THIS TIME";I$
17865 IF I$="N" THEN 18250
17870 PRINT USING "& ,YOU HAVE ### HP AND $ ####";N$(PL);P(PL,1);SV(PL)
17890 OPEN "I",#1,"ENGINEX3"
17900 SM$=MFR$(PL)
17910 IF SM$="" THEN INPUT "MANUFACTURER";SM$
17920 SP3=P(PL,3)
17930 SP1=0
17940 WHILE NOT EOF(1)
17950 INPUT#1,MFGR$,P1,P2,P3,P4,P5,P6,P7,NC,NB,CR,CAM$,BRTH
17960 IF SM$=MFGR$ THEN 17970 ELSE 18060
17970 IF SP3>0 THEN 17980 ELSE 18010
17980 IF SP3=P3 THEN 17990 ELSE 18060
17990 IF SP1>0 THEN 18000 ELSE 18010
18000 IF SP1=P1 THEN 18010 ELSE 18060
18010 PRINT USING "MFGR &, DISP=###, HP=### @ ####, TORQUE=### @ ####";MFGR$;P3;P1;P5;P6;P7
18020 LL=LL+1
18030 IF LL<23 THEN 18060
18040 INPUT "PRESS ENTER FOR MORE";LX$
18050 LL=1
18060 WEND
18070 CLOSE #1
18080 INPUT "WHAT IS THE EXACT HORSEPOWER YOU WANT";WP1
18085 IF WP1=0 THEN 18260
18090 WORK1=WP1-P(PL,1)
18100 WORK2=WORK1*MD
18110 WORK3=SV(PL)-WORK2
18120 IF WORK3<0 THEN PRINT "YOU CAN'T AFFORD THAT - TRY AGAIN":GOTO 18080
18130 PRINT USING "YOU CAN INCREASE TO ### HP - IT WILL LEAVE YOU WITH $ ####";WP1;WORK3
18140 INPUT "ENTER 'Y' TO CONFIRM MODIFICATION";I$
18150 IF I$="Y" THEN 18170
18160 PRINT "YOU OPTED NOT TO MODIFY AT THIS TIME":GOTO 18250
18170 P(PL,1)=WP1:SV(PL)=WORK3:VL(PL)=VL(PL)+(WORK1*2)
18180 PRINT:PRINT USING "MODIFICATIONS COMPLETE; HP = ###, CASH = $ ####, CAR IS WORTH $ ####";P(PL,1);SV(PL);VL(PL)
18200 P(L,2)=0:P(L,5)=0:P(L,6)=0:P(L,7)=0
18210 GOSUB 10200
18220 GOSUB 8900
18230 GOSUB 12020
18240 CLOSE #1
18250 REM
18260 NEXT PL
18270 GOTO 890
18280 STOP
20000 PRINT:PRINT "PRESS C THEN ENTER IF YOU WANT THE COMPUTER TO CHOOSE"
20010 PRINT:PRINT
20020 RETURN
20050 PRINT "BE SURE YOU CHOOSE A NUMBER BETWEEN THE SPECIFIED RANGES.                       TYPE YOUR NUMBER AND PRESS ENTER"
20060 PRINT:RETURN
20100 PRINT:PRINT
20110 PRINT "You must remember the number of the car you want because you can only read the  list one time."
20120 PRINT:PRINT
20125 PRINT:INPUT "Press the enter key to continue";I$
20130 RETURN
20150 PRINT:PRINT
20160 PRINT "Type the number and press the enter key."
20170 PRINT:PRINT
20180 RETURN
